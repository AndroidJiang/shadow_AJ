buildscript {//这个模块要放在plugins {} 之前
    repositories {
        google()
        jcenter()
    }

    dependencies {
        classpath 'com.tencent.shadow.core:runtime'
        classpath 'com.tencent.shadow.core:activity-container'
        classpath 'com.tencent.shadow.core:gradle-plugin'
        classpath 'org.javassist:javassist:3.22.0-GA'
    }
}


apply plugin: 'com.android.application'
apply plugin: 'com.tencent.shadow.plugin'
apply plugin: 'org.jetbrains.kotlin.android'

android {
    namespace 'com.ajiang.plugin_host'
    compileSdk 33

    defaultConfig {
        applicationId "com.ajiang.plugin_host"
        minSdk 24
        targetSdk 33
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    aaptOptions {
        additionalParameters "--package-id", "0x7E", "--allow-reserved-package-id"
    }

}

dependencies {

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.2.0'
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'com.google.android.material:material:1.6.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'

    //如果不以compileOnly方式依赖，会导致其他Transform或者Proguard找不到这些类
    compileOnly 'com.tencent.shadow.core:runtime'
}


shadow {
    transform {
//   useHostContext = ['abc']
    }

    packagePlugin {
        pluginTypes {
            debug {
                loaderApkConfig = new Tuple2('shadow-loader-debug.apk', ':shadow-loader:assembleDebug')
                runtimeApkConfig = new Tuple2('shadow-runtime-debug.apk', ':shadow-runtime:assembleDebug')
                pluginApks {
                    pluginApk1 {
                        businessName = 'shadow-plugin'
                        partKey = 'shadow-plugin'
                        buildTask = ':shadow-plugin:assembleDebug'
                        apkPath = 'shadow-plugin/build/outputs/apk/plugin/debug/shadow-plugin-plugin-debug.apk'
                    }
                }
            }

            release {
                loaderApkConfig = new Tuple2('shadow-loader-release.apk', ':shadow-loader:assembleRelease')
                runtimeApkConfig = new Tuple2('shadow-runtime-release.apk', ':shadow-runtime:assembleRelease')
                pluginApks {
                    pluginApk1 {
                        businessName = 'shadow-plugin'
                        partKey = 'shadow-plugin'
                        buildTask = ':shadow-plugin:assembleRelease'
                        apkPath = 'shadow-plugin/build/outputs/apk/release/shadow-plugin-release.apk'
                    }
                }
            }
        }

        loaderApkProjectPath = 'shadow-loader'
        runtimeApkProjectPath = 'shadow-runtime'

        version = 1
        compactVersion = [1, 2, 3]
        uuidNickName = "1.0"
    }
}